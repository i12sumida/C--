/*
 * TacOS Source Code
 *    Tokuyama kousen Advanced educational Computer.
 *
 * Copyright (C) 2016 by
 *                      Dept. of Computer Science and Electronic Engineering,
 *                      Tokuyama College of Technology, JAPAN
 *
 *   上記著作権者は，Free Software Foundation によって公開されている GNU 一般公
 * 衆利用許諾契約書バージョン２に記述されている条件を満たす場合に限り，本ソース
 * コード(本ソースコードを改変したものを含む．以下同様)を使用・複製・改変・再配
 * 布することを無償で許諾する．
 *
 *   本ソースコードは＊全くの無保証＊で提供されるものである。上記著作権者および
 * 関連機関・個人は本ソースコードに関して，その適用可能性も含めて，いかなる保証
 * も行わない．また，本ソースコードの利用により直接的または間接的に生じたいかな
 * る損害に関しても，その責任を負わない．
 *
 *
 */

/*
 * ls/ls.cmm : ls プログラム
 *
 * 2016.03.07 : 新規作成
 *
 * $Id$
 *
 */

#include <stdio.hmm>
#include <stdlib.hmm>
#include <syslib.hmm>

Dir dir = {"            ", 0, 0, 0, 0 };        // ディレクトリの読出しバッファ

// ディレクトリの一覧を表示する
int printDir(char[] fname) {
  int fd = open(fname, READ);                   // ディレクトリを開く
  if (fd<0) {
    perror(fname);
    return 1;
  }

  printf("FileNameExt Attr Clst FileLength\n");
  while (readDir(fd, dir)>0) {                  // ファイルが続く間
    printf("%11s 0x%02x %4d 0x%04x%04x\n",      //   ファイルの一覧を出力
             dir.name, dir.attr, dir.clst,
             dir.lenH, dir.lenL);
  }
  close(fd);
  return 0;
}

public int main(int argc, char[][] argv) {
  int err = 0;
  if  (argc==1) {                               // 特に指定がなければ
    err = printDir("/");
  } else {
    for (int i=1; i<argc; i=i+1) {
      if (argc>2) printf("%s:\n", argv[i]);     // 複数ファイルの場合は名前表示
      err = err | printDir(argv[i]);
    }
  }
  return err;
}
